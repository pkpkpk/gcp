(ns gcp.global.protobuf-tests
  (:require [clojure.test :refer :all]
            [clojure.test.check :as tc]
            [clojure.test.check.generators :as gen]
            [clojure.test.check.properties :as prop]
            [gcp.global :as g]
            [gcp.global :as global]
            [gcp.protobuf :as protobuf]
            [malli.core :as m]
            [malli.generator :as mg])
  (:import [com.google.protobuf ByteString Duration]
           [java.nio ByteBuffer]))

#_ (do (require :reload 'gcp.global.protobuf-tests)
       (in-ns 'gcp.global.protobuf-tests))

(def byte-string-test-generator
  (gen/one-of [(gen/fmap #(ByteString/copyFromUtf8 %) gen/string-alphanumeric)
               (gen/fmap #(ByteString/copyFrom ^bytes %) (gen/fmap byte-array (gen/vector gen/byte 0 100)))]))

(def byte-string-edn-generator
  (gen/one-of [gen/string-alphanumeric
               (gen/fmap byte-array (gen/vector gen/byte 0 100))]))

;; TODO test values generated from schema rt too
(defn do-bytestring-value-test [n]
  (testing "do generated values roundtrip through to-edn and from-edn"
    (let [prop (prop/for-all [v byte-string-test-generator]
                 (let [edn (protobuf/bytestring-to-edn v)]
                   (and (global/valid? ::protobuf/ByteString edn)
                        (.equals ^ByteString v (protobuf/bytestring-from-edn edn)))))
          {:keys [pass?] :as res} (tc/quick-check n prop)]
      (when-not pass?
        res))))

(defn do-bytestring-schema-test [n]
  (testing "do all values generated by the schema produce valid instances via from-edn"
    (let [prop (prop/for-all [edn (mg/generator [:and
                                                 {:gen/gen byte-string-edn-generator}
                                                 :gcp.protobuf/ByteString]
                                              (global/mopts))]
                 (let [bs (protobuf/bytestring-from-edn edn)]
                   (and (g/valid? ::protobuf/ByteString edn)
                        (instance? ByteString bs)
                        (= (protobuf/bytestring-to-edn bs)))))
          {:keys [pass?] :as res} (tc/quick-check n prop)]
      (when-not pass?
        res))))

(deftest bytestring-test
  (is (nil? (do-bytestring-value-test 100)))
  (is (nil? (do-bytestring-schema-test 100))))

#!----------------------------------------------------------------------------------------------------

(def duration-test-generator
  (gen/fmap (fn [[seconds nanos]]
              (-> (Duration/newBuilder)
                  (.setSeconds seconds)
                  (.setNanos nanos)
                  (.build)))
            (gen/tuple (gen/choose 0 Short/MAX_VALUE) (gen/choose 0 Short/MAX_VALUE))))

(defn do-duration-value-test [n]
  (testing "do generated values roundtrip through to-edn and from-edn"
    (let [prop (prop/for-all [v duration-test-generator]
                 (let [edn (protobuf/Duration-to-edn v)]
                   (and (global/valid? ::protobuf/Duration edn)
                        (.equals v (protobuf/Duration-from-edn edn)))))
          {:keys [pass?] :as res} (tc/quick-check n prop)]
      (when-not pass?
        res))))

(defn do-duration-schema-test [n]
  (testing "do all values generated by the schema produce valid instances via from-edn"
    (let [prop (prop/for-all [v (mg/generator :gcp.protobuf/Duration (global/mopts))]
                 (let [res (protobuf/Duration-from-edn v)]
                   (instance? Duration res)))
          {:keys [pass?] :as res} (tc/quick-check n prop)]
      (when-not pass?
        res))))

(deftest duration-test
  (is (nil? (do-duration-value-test 100)))
  (is (nil? (do-duration-schema-test 100))))

;; ListValue
;; Value
;; Struct
;; ProtocolStringList